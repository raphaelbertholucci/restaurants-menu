// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'jacoco'
defaultTasks 'dependencyUpdates'

buildscript {
    apply from: 'dependencies.gradle'

    def properties = new Properties()
    file('credentials.properties').withInputStream { properties.load(it) }
    ext.apiKey = properties.getProperty('api.key')

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${gradle_version}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.39.0'
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                    reject('Release candidate')
                }
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion versions.compileSdk
                buildToolsVersion versions.buildTools

                defaultConfig {
                    applicationId application.appicationId
                    minSdkVersion versions.minSdk
                    targetSdkVersion versions.targetSdk
                    versionCode application.versionCode
                    versionName application.versionName

                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                sourceSets {
                    main.java.srcDirs += 'src/main/kotlin/'
                    test.java.srcDirs += 'src/test/kotlin'
                    androidTest.java.srcDirs += 'src/androidTest/kotlin'
                }

                packagingOptions {
                    exclude "**/attach_hotspot_windows.dll"
                    exclude "META-INF/licenses/**"
                    exclude "META-INF/AL2.0"
                    exclude "META-INF/LGPL2.1"
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}